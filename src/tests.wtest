import wollok.game.*
import configuracion.*
import direcciones.*
import juego.*
import main.*
import niveles.*
import nivel1.*
import nivel2.*
import nivel3.*
import nivel4.*
import nivel5.*

describe "Piramide De Maslow" {
	
	test "Si el personaje fuerte interactua con la caja, éste se mantiene en su posición"
	{
		nivel1.reiniciarse()
		personajeFuerte.position(game.at(2, 5))
		personajeFuerte.moverA(arriba)
		assert.that(personajeFuerte.position() == game.at(2,5))
	}
	
	test "Si el personaje se mueve hacia abajo, no puede porque hay una pared"
	{
		game.clear()
		game.addVisual(personajeFuerte)
		personajeFuerte.position(game.at(3, 5))
		const pared = new Pared(position = game.at(3, 4))
		game.addVisual(pared)
		personajeFuerte.moverA(abajo)
		assert.equals(personajeFuerte.position(), game.at(3,5)) 
	}
	
	test "El personaje inteligente no puede mover la caja"
	{
		nivel1.reiniciarse()
		personajeInteligente.position(game.at(5,4))
		const caja = new Caja(posicionInicial = game.at(5, 5))
		game.addVisual(caja)
		assert.throwsException({personajeInteligente.moverA(arriba)})
	}
	 
	test "Si el personaje esta en la misma posicion que la placa, ésta se activa"
	{
		nivel1.reiniciarse()
		personajeInteligente.position(game.at(8,4))
		personajeInteligente.moverA(derecha)
		personajeFuerte.position(game.at(7,9))
		assert.that(nivel1.placas().all{placa => placa.activado()})
	}
	
	test "Si un personaje pisa los pinches pierde una vida"
	{
		nivel1.reiniciarse()
		personajeFuerte.position(game.at(7,7))
		personajeFuerte.moverA(arriba)
		assert.equals(gestorNiveles.vidas(),2)
	}
	
	test "Si un personaje pisa los superpinches pierde dos vidas"
	{
		nivel1.reiniciarse()
		const pinche = new SuperPinche()
		game.addVisual(pinche)
		pinche.position(game.at(7,6))
		personajeFuerte.position(game.at(7,5))
		personajeFuerte.moverA(arriba)
		assert.equals(gestorNiveles.vidas(),1)
	}
	
	test "Si se pierden todas las vidas, el juego finaliza"
	{
		gestorNiveles.perderVida()
		gestorNiveles.perderVida()
		assert.throwsException({gestorNiveles.perderVida()})
	}

	test "Si el personaje llega a la comida pasa al siguiente nivel"
	{
		nivel1.reiniciarse()
		nivel1.objetoGanador().colisionarConPersonaje(personajeInteligente)
		assert.that(gestorNiveles.nivelActualNumero() == 2)
	}
}